Precise Hazel Coyote

medium

# Rolling over a loan using `FlashRolloverLoan_G5.sol` will always revert

## Summary

There is a function selector (`ISmartCommitmentForwarder.acceptSmartCommitmentWithRecipient.selector`) with no implementation that will revert anytime it is called.

## Vulnerability Detail

When a user attempts to call `FlashRolloverLoan_G5::rolloverLoanWithFlash()` and provide `smartCommitmentAddress that is not 0`, it will revert due to line 120 in FlashRolloverLoan_G5.sol. 

```solidity
function rolloverLoanWithFlash(
        address _lenderCommitmentForwarder,
        uint256 _loanId,
        uint256 _flashLoanAmount,
        uint256 _borrowerAmount, //an additional amount borrower may have to add
        AcceptCommitmentArgs calldata _acceptCommitmentArgs
    ) external   {
        address borrower = TELLER_V2.getLoanBorrower(_loanId);
        require(borrower == msg.sender, "CommitmentRolloverLoan: not borrower");

        // Get lending token and balance before
        address lendingToken = TELLER_V2.getLoanLendingToken(_loanId);

        if (_borrowerAmount > 0) {
            IERC20(lendingToken).transferFrom(
                borrower,
                address(this),
                _borrowerAmount
            );
        }

        // Call 'Flash' on the vault to borrow funds and call tellerV2FlashCallback
        // This ultimately calls executeOperation
        IPool(POOL()).flashLoanSimple(
            address(this),
            lendingToken,
            _flashLoanAmount,
            abi.encode(
                RolloverCallbackArgs({
                    lenderCommitmentForwarder :_lenderCommitmentForwarder,
                    loanId: _loanId,
                    borrower: borrower,
                    borrowerAmount: _borrowerAmount,
                    acceptCommitmentArgs: abi.encode(_acceptCommitmentArgs)
                })
            ),
            0 //referral code
        );
    }
```
This is due to the `POOL()` contract calling the `executeOperation()` hook. 
```solidity
    ...
        AcceptCommitmentArgs memory acceptCommitmentArgs = abi.decode(
            _rolloverArgs.acceptCommitmentArgs,
            (AcceptCommitmentArgs)
        );

        // Accept commitment and receive funds to this contract

        (uint256 newLoanId, uint256 acceptCommitmentAmount) = _acceptCommitment(
            _rolloverArgs.lenderCommitmentForwarder,
            _rolloverArgs.borrower,
            _flashToken,
            acceptCommitmentArgs
        );
    ...
```

When this hook is called `FlashRolloverLoan_G5::_acceptCommitment()` is attempted. The contract then makes a `functionCall()` to the `lenderCommitmentForwarder` with data that specifies the function selector for `acceptSmartCommitmentWithRecipient()`. There are multiple commitment forwarders and this function selector is only found in `ISmartCommitmentForwarder.sol`. It is not found in `SmartCommitmentForwarder.sol`, and with no implementation this function call will revert.

```solidity
function _acceptCommitment(
        address lenderCommitmentForwarder,
        address borrower,
        address principalToken,
        AcceptCommitmentArgs memory _commitmentArgs
    )
        internal
        virtual
        returns (uint256 bidId_, uint256 acceptCommitmentAmount_)
    {
        uint256 fundsBeforeAcceptCommitment = IERC20Upgradeable(principalToken)
            .balanceOf(address(this));



        if (_commitmentArgs.smartCommitmentAddress != address(0)) {

             bytes memory responseData = address(lenderCommitmentForwarder)
                    .functionCall(
                        abi.encodePacked(
                            abi.encodeWithSelector(
                                ISmartCommitmentForwarder
                                    .acceptSmartCommitmentWithRecipient
                                    .selector,
                                _commitmentArgs.smartCommitmentAddress,
                                _commitmentArgs.principalAmount,
                                _commitmentArgs.collateralAmount,
                                _commitmentArgs.collateralTokenId,
                                _commitmentArgs.collateralTokenAddress,
                                address(this),
                                _commitmentArgs.interestRate,
                                _commitmentArgs.loanDuration
                            ),
                            borrower //cant be msg.sender because of the flash flow
                        )
                    );

                (bidId_) = abi.decode(responseData, (uint256));


        }
```

## Impact

Users will not be able to flash rollover loans using any of the Smart Commitment Contracts.

## Code Snippet

https://github.com/sherlock-audit/2024-04-teller-finance/blob/e9747582ac67f2c4ac98d4938c253c36f3db0b49/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/FlashRolloverLoan_G5.sol#L97-L135

https://github.com/sherlock-audit/2024-04-teller-finance/blob/e9747582ac67f2c4ac98d4938c253c36f3db0b49/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/FlashRolloverLoan_G5.sol#L156-L219

https://github.com/sherlock-audit/2024-04-teller-finance/blob/e9747582ac67f2c4ac98d4938c253c36f3db0b49/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/FlashRolloverLoan_G5.sol#L270-L368

## Tool used

Manual Review

## Recommendation

Change the name of `acceptCommitmentWithRecipient()` in `SmartCommitmentForwarder.sol` to match the selector used in `FlashRolloverLoan_G5.sol`
