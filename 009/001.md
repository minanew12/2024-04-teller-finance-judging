Cheerful Orange Kestrel

high

# The reputation will be updated even if the loan is not fully repaid

## Summary

The `_repayLoan` function should update the reputation when the loan is fully repaid and has a mark. However, the current implementation only checks if the borrower has a mark, without verifying whether the loan is fully repaid.

## Vulnerability Detail

According to the comment in the `_repayLoan` function, the reputation should be updated only when the loan is fully repaid and has a mark. However, the code currently checks only if the borrower has a mark, without verifying whether the loan is fully repaid.

```solidity
    function _repayLoan(
        uint256 _bidId,
        Payment memory _payment,
        uint256 _owedAmount,
        bool _shouldWithdrawCollateral
    ) internal virtual {
        Bid storage bid = bids[_bidId];
        uint256 paymentAmount = _payment.principal + _payment.interest;

        RepMark mark = reputationManager.updateAccountReputation(
            bid.borrower,
            _bidId
        );

        // Check if we are sending a payment or amount remaining
        if (paymentAmount >= _owedAmount) {
            paymentAmount = _owedAmount;

            if (bid.state != BidState.LIQUIDATED) {
                bid.state = BidState.PAID;
            }

            // Remove borrower's active bid
            _borrowerBidsActive[bid.borrower].remove(_bidId);

            // If loan is is being liquidated and backed by collateral, withdraw and send to borrower
            if (_shouldWithdrawCollateral) {
                //   _getCollateralManagerForBid(_bidId).withdraw(_bidId);
                collateralManager.withdraw(_bidId);
            }

            emit LoanRepaid(_bidId);
        } else {
            emit LoanRepayment(_bidId);
        }

        _sendOrEscrowFunds(_bidId, paymentAmount); //send or escrow the funds

        // update our mappings
        bid.loanDetails.totalRepaid.principal += _payment.principal;
        bid.loanDetails.totalRepaid.interest += _payment.interest;
        bid.loanDetails.lastRepaidTimestamp = uint32(block.timestamp);
//@audit the repuation can be updated even if the loan is not fully paid
        // If the loan is paid in full and has a mark, we should update the current reputation
        if (mark != RepMark.Good) {
            reputationManager.updateAccountReputation(bid.borrower, _bidId);
        }
    }
```

## Impact

The reputation staus of borrower can be updated even if they have not fully paid the loan.

## Code Snippet

https://github.com/sherlock-audit/2024-04-teller-finance/blob/e9747582ac67f2c4ac98d4938c253c36f3db0b49/teller-protocol-v2-audit-2024/packages/contracts/contracts/TellerV2.sol#L850-L897

## Tool used

Manual Review

## Recommendation

Update the reputation when the loan is fully repaid.
```diff
diff --git a/teller-protocol-v2-audit-2024/packages/contracts/contracts/TellerV2.sol b/teller-protocol-v2-audit-2024/packages/contracts/contracts/TellerV2.sol
index d288836..2f79342 100644
--- a/teller-protocol-v2-audit-2024/packages/contracts/contracts/TellerV2.sol
+++ b/teller-protocol-v2-audit-2024/packages/contracts/contracts/TellerV2.sol
@@ -878,6 +878,10 @@ contract TellerV2 is
                 collateralManager.withdraw(_bidId);
             }

+        // If the loan is paid in full and has a mark, we should update the current reputation
+        if (mark != RepMark.Good) {
+            reputationManager.updateAccountReputation(bid.borrower, _bidId);
+        }
             emit LoanRepaid(_bidId);
         } else {
             emit LoanRepayment(_bidId);
@@ -890,10 +894,6 @@ contract TellerV2 is
         bid.loanDetails.totalRepaid.interest += _payment.interest;
         bid.loanDetails.lastRepaidTimestamp = uint32(block.timestamp);

-        // If the loan is paid in full and has a mark, we should update the current reputation
-        if (mark != RepMark.Good) {
-            reputationManager.updateAccountReputation(bid.borrower, _bidId);
-        }
     }

     function _sendOrEscrowFunds(uint256 _bidId, uint256 _paymentAmount)
```