Careful Red Yeti

high

# `lenderCommitmentGroup_smart.liquidateDefaultedLoanWithIncentive()` will not give collateral to caller

## Summary

## Vulnerability Detail
`lenderCommitmentGroup_smart.liquidateDefaultedLoanWithIncentive()` is supposed to give collaterals of a defaulted loan to the caller of the tx as incentive for liquidating the defaulted loans.
```solidity
 function liquidateDefaultedLoanWithIncentive(
        uint256 _bidId,
        int256 _tokenAmountDifference
    ) public bidIsActiveForGroup(_bidId) {
        uint256 amountDue = getAmountOwedForBid(_bidId, false);

        uint256 loanDefaultedTimeStamp = ITellerV2(TELLER_V2)
            .getLoanDefaultTimestamp(_bidId);

        int256 minAmountDifference = getMinimumAmountDifferenceToCloseDefaultedLoan(
                amountDue,
                loanDefaultedTimeStamp
            );

        require(
            _tokenAmountDifference >= minAmountDifference,
            "Insufficient tokenAmountDifference"
        );

        if (_tokenAmountDifference > 0) {
            //this is used when the collateral value is higher than the principal (rare)
            //the loan will be completely made whole and our contract gets extra funds too
            uint256 tokensToTakeFromSender = abs(_tokenAmountDifference);

            IERC20(principalToken).transferFrom(
                msg.sender,
                address(this),
                amountDue + tokensToTakeFromSender
            );

            tokenDifferenceFromLiquidations += int256(tokensToTakeFromSender);

            totalPrincipalTokensRepaid += amountDue;
        } else {
           
            uint256 tokensToGiveToSender = abs(_tokenAmountDifference);

            IERC20(principalToken).transferFrom(
                msg.sender,
                address(this),
                amountDue - tokensToGiveToSender
            );

            tokenDifferenceFromLiquidations -= int256(tokensToGiveToSender);

            totalPrincipalTokensRepaid += amountDue;
        }

        //this will give collateral to the caller
        ITellerV2(TELLER_V2).lenderCloseLoanWithRecipient(_bidId, msg.sender);// @audit-issue 5. this won't give collateral to msg.sender but instead it will give it to this contract.
    } // lenderCommitmentGroup_smart.sol contract will receive collateral whenever liquidateDefaultedLoanWithIncentive() is called

```
But there's an issue in the codeflow that makes lenderCommitmentGroup_smart.sol contract receive the collaterals instead.

The issue lies in `TellerV2._lenderCloseLoanWithRecipient()`. 
```solidity

    function _lenderCloseLoanWithRecipient(
        uint256 _bidId,
        address _collateralRecipient
    ) internal acceptedLoan(_bidId, "lenderClaimCollateral") {
        require(isLoanDefaulted(_bidId), "Loan must be defaulted.");

        Bid storage bid = bids[_bidId];
        bid.state = BidState.CLOSED;

        address sender = _msgSenderForMarket(bid.marketplaceId);
        require(sender == bid.lender, "only lender can close loan");
       
        /*


          address collateralManagerForBid = address(_getCollateralManagerForBid(_bidId)); 

          if( collateralManagerForBid == address(collateralManagerV2) ){
             ICollateralManagerV2(collateralManagerForBid).lenderClaimCollateral(_bidId,_collateralRecipient);
          }else{
             require( _collateralRecipient == address(bid.lender));
             ICollateralManager(collateralManagerForBid).lenderClaimCollateral(_bidId );
          }
          
          */

        collateralManager.lenderClaimCollateral(_bidId);

        emit LoanClosed(_bidId);
    }
```
The` _collateralRecipient` param in `TellerV2._lenderCloseLoanWithRecipient()` is apparently useless, because its not used to do anything at all within the logic of the function. 

Now in the external call to  `collateralManager.lenderClaimCollateral()`, collaterals are sent to the actual lenders which will be lenderCommitmentGroup_smart.sol contract in this case.
```solidity
 function lenderClaimCollateral(uint256 _bidId) external onlyTellerV2 {
        if (isBidCollateralBacked(_bidId)) {
            BidState bidState = tellerV2.getBidState(_bidId);

            require(
                bidState == BidState.CLOSED,
                "Loan has not been liquidated"
            );

            _withdraw(_bidId, tellerV2.getLoanLender(_bidId));
            emit CollateralClaimed(_bidId);
        }
    }
```
 lenderCommitmentGroup_smart.sol contract  doesn't have any way of sending out the collaterals received so its stuck 
## Impact
1. Liquidators of defaulted loans via `lenderCommitmentGroup_smart.liquidateDefaultedLoanWithIncentive()` won't receive any incentive

2. There's no logic to remove the received collateral in lenderCommitmentGroup_smart.sol contract, so the collateral will be forever stuck in the lenderCommitmentGroup_smart.sol contract.
## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L471

https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/TellerV2.sol#L747

https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/CollateralManager.sol#L280
## Tool used

Manual Review

## Recommendation
Maybe look for a way to pass `_collateralRecipient` into `collateralManager.lenderClaimCollateral()`