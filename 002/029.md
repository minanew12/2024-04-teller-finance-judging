Atomic Eggshell Puppy

high

# It is impossible for a lender to close loan if they've claimed the loan NFT

## Summary
It is impossible for a lender to close loan if they've claimed the loan NFT

## Vulnerability Detail
When a lender has given a loan, they can claim the loan's NFT. This sets `loan.lender` to a placeholder value and mints an NFT which represents ownership to the lender.
```solidity
    function claimLoanNFT(uint256 _bidId)
        external
        acceptedLoan(_bidId, "claimLoanNFT")
        whenNotPaused
    {
        // Retrieve bid
        Bid storage bid = bids[_bidId];

        address sender = _msgSenderForMarket(bid.marketplaceId);
        require(sender == bid.lender, "only lender can claim NFT");

        // set lender address to the lender manager so we know to check the owner of the NFT for the true lender
        bid.lender = address(USING_LENDER_MANAGER);

        // mint an NFT with the lender manager
        lenderManager.registerLoan(_bidId, sender);
    }
```

For this reason, any time a loan's lender needs to be retrieved, it should happen via the `getLoanLender` function 
```solidity
    function getLoanLender(uint256 _bidId)
        public
        view
        returns (address lender_)
    {
        lender_ = bids[_bidId].lender;

        if (lender_ == address(USING_LENDER_MANAGER)) {
            return lenderManager.ownerOf(_bidId);
        }

        //this is left in for backwards compatibility only
        if (lender_ == address(lenderManager)) {
            return lenderManager.ownerOf(_bidId);
        }
    }
```

However, this does not happen within `_lenderCloseLoanWithRecipient` - there it checks `loan.lender` against the `msg.sender`. In the case that the loan NFT has been claimed, it will be impossible for a lender to close a loan.

```solidity
    function _lenderCloseLoanWithRecipient(
        uint256 _bidId,
        address _collateralRecipient
    ) internal acceptedLoan(_bidId, "lenderClaimCollateral") {
        require(isLoanDefaulted(_bidId), "Loan must be defaulted.");

        Bid storage bid = bids[_bidId];
        bid.state = BidState.CLOSED;

        address sender = _msgSenderForMarket(bid.marketplaceId);
        require(sender == bid.lender, "only lender can close loan");
```
## Impact
It will be impossible for a lender to close a loan if they've claimed the loan NFT.

## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/TellerV2.sol#L745C1-L755C69

## Tool used

Manual Review

## Recommendation
use `getLoanLender` 
