Fun Blonde Chicken

high

# The collateral Token is mistakenly given to the lender when the liquidator call liquidateDefaultedLoanWithIncentive()


## Summary
The collateral Token is mistakenly given to the lender when the liquidator call `liquidateDefaultedLoanWithIncentive()`.The collateral Token should be given to the liquidator.
## Vulnerability Detail
```javascript
    function liquidateDefaultedLoanWithIncentive(
        uint256 _bidId,
        int256 _tokenAmountDifference
    ) public bidIsActiveForGroup(_bidId) {
        uint256 amountDue = getAmountOwedForBid(_bidId, false);
        uint256 loanDefaultedTimeStamp = ITellerV2(TELLER_V2)
            .getLoanDefaultTimestamp(_bidId);

        int256 minAmountDifference = getMinimumAmountDifferenceToCloseDefaultedLoan(
                amountDue,
                loanDefaultedTimeStamp
            );

        require(
            _tokenAmountDifference >= minAmountDifference,
            "Insufficient tokenAmountDifference"
        );

        if (_tokenAmountDifference > 0) {
            //this is used when the collateral value is higher than the principal (rare)
            //the loan will be completely made whole and our contract gets extra funds too
            uint256 tokensToTakeFromSender = abs(_tokenAmountDifference);

            IERC20(principalToken).transferFrom(
                msg.sender,
                address(this),
                amountDue + tokensToTakeFromSender
            );

            tokenDifferenceFromLiquidations += int256(tokensToTakeFromSender);

            totalPrincipalTokensRepaid += amountDue;
        } else {
           
            uint256 tokensToGiveToSender = abs(_tokenAmountDifference);

            IERC20(principalToken).transferFrom(
                msg.sender,
                address(this),
                amountDue - tokensToGiveToSender
            );

            tokenDifferenceFromLiquidations -= int256(tokensToGiveToSender);

            totalPrincipalTokensRepaid += amountDue;
        }
        //this will give collateral to the caller
@>       ITellerV2(TELLER_V2).lenderCloseLoanWithRecipient(_bidId, msg.sender);
    }
```
However,the collateral will be given to caller not the second parameter `msg.sender` In `TELLER_V2::lenderCloseLoanWithRecipient()`. The `lenderCloseLoanWithRecipient()` calls `_lenderCloseLoanWithRecipient`, we can see that the second parameter `_collateralRecipient` never be used.
```javascript
 function _lenderCloseLoanWithRecipient(
        uint256 _bidId,
        address _collateralRecipient
    ) internal acceptedLoan(_bidId, "lenderClaimCollateral") {
        require(isLoanDefaulted(_bidId), "Loan must be defaulted.");

        Bid storage bid = bids[_bidId];
        bid.state = BidState.CLOSED;

        address sender = _msgSenderForMarket(bid.marketplaceId);
        require(sender == bid.lender, "only lender can close loan");

        /*


          address collateralManagerForBid = address(_getCollateralManagerForBid(_bidId)); 

          if( collateralManagerForBid == address(collateralManagerV2) ){
             ICollateralManagerV2(collateralManagerForBid).lenderClaimCollateral(_bidId,_collateralRecipient);
          }else{
             require( _collateralRecipient == address(bid.lender));
             ICollateralManager(collateralManagerForBid).lenderClaimCollateral(_bidId );
          }
          
          */

@>        collateralManager.lenderClaimCollateral(_bidId);

        emit LoanClosed(_bidId);
    }
```
POC:
- 1, A borrower borrows 1e18 WETH
- 2, after the default time plus 86400 seconds, the liquidator can liquidate the defaulted loan only with 1e18 WETH
- 3, we can see that lender get the collateral and liquidator get nothing.
```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;
import { Testable } from "../../../Testable.sol";

//import { LenderCommitmentGroup_Smart_Override } from "./LenderCommitmentGroup_Smart_Override.sol";
import {LenderCommitmentGroup_Smart} from "../../../../contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol";
import {TestERC20Token} from "../../../tokens/TestERC20Token.sol";
import "../../../../contracts/mock/WethMock.sol";
import "../../../../contracts/interfaces/IWETH.sol";

import {TellerV2SolMock} from "../../../../contracts/mock/TellerV2SolMock.sol";

import {UniswapV3PoolMock} from "../../../../contracts/mock/uniswap/UniswapV3PoolMock.sol";
import {UniswapV3FactoryMock} from "../../../../contracts/mock/uniswap/UniswapV3FactoryMock.sol";
import { PaymentType, PaymentCycleType } from "../../../../contracts/libraries/V2Calculations.sol";
import { LoanDetails, Payment, BidState , Bid, Terms } from "../../../../contracts/TellerV2Storage.sol";
import {console, Test} from  "lib/forge-std/src/Test.sol";


import { SmartCommitmentForwarder} from "../../../../contracts/LenderCommitmentForwarder/SmartCommitmentForwarder.sol";
import { TellerV2 } from "../../../../contracts/TellerV2.sol";
import { MarketRegistry } from "../../../../contracts/MarketRegistry.sol";
import { ReputationManager } from "../../../../contracts/ReputationManager.sol";
import "../../../../contracts/interfaces/IMarketRegistry.sol";
import "../../../../contracts/interfaces/IReputationManager.sol";
import {CollateralEscrowV1} from "../../../../contracts/escrow/CollateralEscrowV1.sol";
import  {UpgradeableBeacon} from  "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {CollateralManager} from "../../../../contracts/CollateralManager.sol";
import { Collateral } from "../../../../contracts/interfaces/escrow/ICollateralEscrowV1.sol";
import { LenderManager } from "../../../../contracts/LenderManager.sol";
import { EscrowVault } from "../../../../contracts/EscrowVault.sol";
import {LenderCommitmentForwarder_G1} from "../../../../contracts/LenderCommitmentForwarder/LenderCommitmentForwarder_G1.sol";

import { User } from "../../../Test_Helpers.sol";

contract LenderCommitmentGroup_Smart_Real_Test is Test {

    TellerV2User private marketOwner;
    TellerV2User private extensionContract;

    TellerV2User private borrower;
    TellerV2User private lender;
    TellerV2User private liquidator;

    TestERC20Token principalToken;
    TestERC20Token collateralToken;
    WethMock wethMock;

    LenderCommitmentGroup_Smart lenderCommitmentGroupSmart;
    TellerV2 tellerV2;
    CollateralManager collateralManager;

    uint256 marketId1;
    
    //TellerV2SolMock _tellerV2;
    SmartCommitmentForwarder smartCommitmentForwarder;
    UniswapV3PoolMock _uniswapV3Pool;
    UniswapV3FactoryMock _uniswapV3Factory;

    function setUp() public {
        vm.warp(0);
        wethMock = new WethMock();
     
////////////////////////////////////////////////////////////////////////////////////
        // Deploy Escrow beacon
        CollateralEscrowV1 escrowImplementation = new CollateralEscrowV1();
        UpgradeableBeacon escrowBeacon = new UpgradeableBeacon(
            address(escrowImplementation)
        );

        // Deploy protocol
        tellerV2 = new TellerV2(address(0));

        // Deploy MarketRegistry & ReputationManager
        IMarketRegistry marketRegistry = IMarketRegistry(new MarketRegistry());
        IReputationManager reputationManager = IReputationManager(
            new ReputationManager()
        );
        reputationManager.initialize(address(tellerV2));

        // Deploy Collateral manager
        collateralManager = new CollateralManager();
        collateralManager.initialize(address(escrowBeacon), address(tellerV2));

        // Deploy Lender manager
        // MetaForwarder metaforwarder = new MetaForwarder();
        // metaforwarder.initialize();
        LenderManager lenderManager = new LenderManager((marketRegistry));
        lenderManager.initialize();
        lenderManager.transferOwnership(address(tellerV2));

        EscrowVault escrowVault = new EscrowVault();
        escrowVault.initialize();

        // Deploy LenderCommitmentForwarder
        LenderCommitmentForwarder_G1 lenderCommitmentForwarder = new LenderCommitmentForwarder_G1(
                address(tellerV2),
                address(marketRegistry)
            );

        // Initialize protocol
        tellerV2.initialize(
            50,
            address(marketRegistry),
            address(reputationManager),
            address(lenderCommitmentForwarder),
            address(collateralManager),
            address(lenderManager),
            address(escrowVault)
        );

       
        // Instantiate users & balances
        marketOwner = new TellerV2User(address(tellerV2), wethMock);
        borrower = new TellerV2User(address(tellerV2), wethMock);
        lender = new TellerV2User(address(tellerV2), wethMock);
        liquidator = new TellerV2User(address(tellerV2), wethMock);


    // Create a market
        marketId1 = marketOwner.createMarket(
            address(marketRegistry),
            86400,    // _paymentCycleDuration,
            600, //_paymentDefaultDuration
            86400, //_bidExpirationTime
            500, //_feePercent
            false, //_requireLenderAttestation
            false, //_requireBorrowerAttestation
            PaymentType.EMI,
            PaymentCycleType.Seconds,
            ""
        );
    
////////////////////////////////////////////////////////////////////////////////////////
    
        smartCommitmentForwarder = new SmartCommitmentForwarder(address(tellerV2), address(marketRegistry));
        _uniswapV3Pool = new UniswapV3PoolMock();

        _uniswapV3Factory = new UniswapV3FactoryMock();
        _uniswapV3Factory.setPoolMock(address(_uniswapV3Pool));
 

        principalToken = TestERC20Token(address(wethMock)); //new TestERC20Token("wrappedETH", "WETH", 1e24, 18);
        wethMock.deposit{value: 1e24}();

        collateralToken = new TestERC20Token("PEPE", "pepe", 1e24, 18);

        principalToken.transfer(address(lender), 1e18);
        collateralToken.transfer(address(borrower), 1e18);


        _uniswapV3Pool.set_mockToken0(address(principalToken));
        _uniswapV3Pool.set_mockToken1(address(collateralToken));

        lenderCommitmentGroupSmart = new LenderCommitmentGroup_Smart(
            address(tellerV2),
            address(smartCommitmentForwarder),
            address(_uniswapV3Factory)
        );
    }

    function initialize_group_contract() public {
        address _principalTokenAddress = address(principalToken);
        address _collateralTokenAddress = address(collateralToken);
        uint32 _maxLoanDuration = 5000000;
        uint16 _minInterestRate = 100;
        uint16 _maxInterestRate = 800;
        uint16 _liquidityThresholdPercent = 10000;
        uint16 _loanToValuePercent = 10000;
        uint24 _uniswapPoolFee = 3000;
        uint32 _twapInterval = 5;
        
        //set trust
        vm.prank(address(marketOwner));
        tellerV2.setTrustedMarketForwarder(marketId1, address(smartCommitmentForwarder));
        
        address _poolSharesToken = lenderCommitmentGroupSmart.initialize(
            _principalTokenAddress,
            _collateralTokenAddress,
            marketId1,
            _maxLoanDuration,
            _minInterestRate,
            _maxInterestRate,
            _liquidityThresholdPercent,
            _loanToValuePercent,
            _uniswapPoolFee,
            _twapInterval
        );

    }
  
      function test_liquidateDefaultedLoanWithIncentive_mistakenly_given() public {


         initialize_group_contract();

         //lender deposit principal token in pool
        vm.startPrank(address(lender)); 
        principalToken.approve(address(lenderCommitmentGroupSmart), 1e18);
        lenderCommitmentGroupSmart.addPrincipalToCommitmentGroup(1e18, address(lender));
        vm.stopPrank();

        
        //borrow token form Group Smart Pool
        vm.startPrank(address(borrower));
        tellerV2.approveMarketForwarder(marketId1, address(smartCommitmentForwarder));
        //erc20 approve
        collateralToken.approve(address(collateralManager), 1e18);

      uint256 bidId =  smartCommitmentForwarder.acceptCommitmentWithRecipient(
            address(lenderCommitmentGroupSmart),
             1e18, 
             1e18, 
             0,
              address(collateralToken),
               address(borrower), 
               100, 
               86400);
        vm.stopPrank();

        vm.warp(86400 + 600 + 86400 ); //after 86400 + 600 + 86400 seconds to liquidate

    
        //Preparing for liquidation
        principalToken.transfer(address(liquidator), 1e18);
        vm.startPrank(address(liquidator));
        principalToken.approve(address(lenderCommitmentGroupSmart), 1e18);
        lenderCommitmentGroupSmart.liquidateDefaultedLoanWithIncentive(bidId, 0);
        vm.stopPrank();
        
        //lender get both
        assertEq(principalToken.balanceOf(address(lenderCommitmentGroupSmart)), 1e18);
        assertEq(collateralToken.balanceOf(address(lenderCommitmentGroupSmart)), 1e18);
        //liquidator get nothing
        assertEq(principalToken.balanceOf(address(liquidator)), 0);
        assertEq(collateralToken.balanceOf(address(liquidator)), 0);

      }

}

contract TellerV2User is User {
    WethMock public immutable wethMock;

    constructor(address _tellerV2, WethMock _wethMock) User(_tellerV2) {
        wethMock = _wethMock;
    }

    function depositToWeth(uint256 amount) public {
        wethMock.deposit{ value: amount }();
    }
}
```
Create a new file named LenderCommitmentGroup_Smart_mistakenly_given.sol with code above in the fold `./packages/contracts/tests/LenderCommitmentForwarder/extensions/SmartCommitments`, Then run `forge test --mt test_liquidateDefaultedLoanWithIncentive_mistakenly_given  -vvv` in terminal. you will get:
```bash
[PASS] test_liquidateDefaultedLoanWithIncentive_mistakenly_given() (gas: 2364005)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.17ms (1.28ms CPU time)

Ran 1 test suite in 167.37ms (7.17ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
the liquidator losses money, No one will liquidate the defaulted loan.
## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L422C5-L472C6
## Tool used

Manual Review

## Recommendation
The collateral Token should be given to the liquidator when liquidating