Delightful Flint Armadillo

medium

# Incorrect collateral amount calculation when tick is negative

## Summary
When calculating the `required collateral` for borrowing, `IUniswapV3Pool().observe()` is used to calculate the tick and square root price. The `square root price` is used for the calculation in getting the `collateral tokens amount` equivalent to `principal tokens`. The problem is that the `square root price` will be incorrect when negative ticks are returned via `.observed()` because we don't round down.

## Vulnerability Detail
Here is the function flow:

`LenderCommitmentGroup_Smart::acceptFundsForAcceptBid`
```javascript
    function acceptFundsForAcceptBid(
        address _borrower,
        uint256 _bidId,
        uint256 _principalAmount,
        uint256 _collateralAmount,
        address _collateralTokenAddress,
        uint256 _collateralTokenId,
        uint32 _loanDuration,
        uint16 _interestRate
    ) external onlySmartCommitmentForwarder whenNotPaused {
        require(
            _collateralTokenAddress == address(collateralToken),
            "Mismatching collateral token"
        );
        
        .
        .
        .

@>      uint256 requiredCollateral = getCollateralRequiredForPrincipalAmount(
            _principalAmount
        );

        require(
            (_collateralAmount * STANDARD_EXPANSION_FACTOR) >=
                requiredCollateral,
            "Insufficient Borrower Collateral"
        );
    }
```

```javascript
    function getCollateralRequiredForPrincipalAmount(
        uint256 _principalAmount
    ) public view returns (uint256) {
@>      uint256 baseAmount = _calculateCollateralTokensAmountEquivalentToPrincipalTokens(
                _principalAmount
            );

        return baseAmount.percent(collateralRatio);
    }
```

```javascript
    function _calculateCollateralTokensAmountEquivalentToPrincipalTokens(
        uint256 principalTokenAmountValue
    ) internal view returns (uint256 collateralTokensAmountToMatchValue) {
        //same concept as zeroforone
        (address token0, ) = _getPoolTokens();

        bool principalTokenIsToken0 = (address(principalToken) == token0);

@>      uint256 pairPriceWithTwap = _getUniswapV3TokenPairPrice(twapInterval);
        uint256 pairPriceImmediate = _getUniswapV3TokenPairPrice(0);

        return
            _getCollateralTokensAmountEquivalentToPrincipalTokens(
                principalTokenAmountValue,
                pairPriceWithTwap,
                pairPriceImmediate,
                principalTokenIsToken0
            );
    }
```

```javascript
    function _getUniswapV3TokenPairPrice(
        uint32 _twapInterval
    ) internal view returns (uint256) {
        // represents the square root of the price of token1 in terms of token0

        uint160 sqrtPriceX96 = getSqrtTwapX96(_twapInterval);

        //this output is the price ratio expanded by 1e18
        return _getPriceFromSqrtX96(sqrtPriceX96);
    }

    function getSqrtTwapX96(
        uint32 twapInterval
    ) public view returns (uint160 sqrtPriceX96) {
        if (twapInterval == 0) {
            // return the current price if twapInterval == 0
            (sqrtPriceX96, , , , , , ) = IUniswapV3Pool(UNISWAP_V3_POOL)
                .slot0();
        } else {
            uint32[] memory secondsAgos = new uint32[](2);
            secondsAgos[0] = twapInterval; // from (before)
            secondsAgos[1] = 0; // to (now)

@>          (int56[] memory tickCumulatives, ) = IUniswapV3Pool(UNISWAP_V3_POOL)
                .observe(secondsAgos);

            // tick(imprecise as it's an integer) to price
@>         sqrtPriceX96 = TickMath.getSqrtRatioAtTick(
                int24(
                    (tickCumulatives[1] - tickCumulatives[0]) /
                        int32(twapInterval)
                )
            );
        }
    }
```

The case where ticks are negative need to be handled, otherwise it will be incorrect, and final calculation of the amount of collateral will be wrong.

[Uniswap library](https://github.com/Uniswap/v3-periphery/blob/main/contracts/libraries/OracleLibrary.sol#L36) handles this edge case the following way: 

```javascript
if (tickCumulativesDelta < 0 && (tickCumulativesDelta % secondsAgo != 0)) arithmeticMeanTick--;
```

## Impact
Incorrect collateral amount calculation. Borrower may need to put up more collateral than is actually required

## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L336

## Tool used
Manual Review

## Recommendation
Incorporate the same method mentioned above by uniswap library to handle this edge case
