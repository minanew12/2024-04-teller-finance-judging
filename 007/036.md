Digital Canvas Horse

medium

# `OwnableUpgradeable` is not initialized in `LenderCommitmentGroup_Smart.sol`

## Summary
`OwnableUpgradeable` is not initialized in `LenderCommitmentGroup_Smart.sol`

## Vulnerability Detail
`LenderCommitmentGroup_Smart.sol` has inherited openzeppelin's `OwnableUpgradeable.sol` contract  for owner access functionality in contract. The used openzeppelin version is `4.8.0`.

```solidity
contract LenderCommitmentGroup_Smart is
    ILenderCommitmentGroup,
    ISmartCommitment,
    ILoanRepaymentListener,
    Initializable,
    OwnableUpgradeable,               @audit // inherited 
    PausableUpgradeable
{
```

The issue here is that, the contract has not initialized the owner which must be done as inherited from OwnableUpgradeable. This can be checked in `initialize()` which is implemented as:

```solidity
    function initialize(
        address _principalTokenAddress,
        address _collateralTokenAddress,
        uint256 _marketId,
        uint32 _maxLoanDuration,
        uint16 _interestRateLowerBound,
        uint16 _interestRateUpperBound,
        uint16 _liquidityThresholdPercent, // When 100% , the entire pool can be drawn for lending.  When 80%, only 80% of the pool can be drawn for lending. 
        uint16 _collateralRatio, //the required overcollateralization ratio.  10000 is 1:1 baseline , typically this is above 10000
        uint24 _uniswapPoolFee,
        uint32 _twapInterval
    ) external initializer returns (address poolSharesToken_) {
        // require(!_initialized,"already initialized");
        // _initialized = true;

        @audit // __Ownable_init() is not initialized which Initializes the OwnableUpgradeable contract setting the deployer as the initial owner

        __Pausable_init();

        principalToken = IERC20(_principalTokenAddress);
        collateralToken = IERC20(_collateralTokenAddress);

        UNISWAP_V3_POOL = IUniswapV3Factory(UNISWAP_V3_FACTORY).getPool(
            _principalTokenAddress,
            _collateralTokenAddress,
            _uniswapPoolFee
        );

        require(UNISWAP_V3_POOL != address(0), "Invalid uniswap pool address");

        marketId = _marketId;

        //in order for this to succeed, first, that SmartCommitmentForwarder needs to be THE trusted forwarder for the market

        ITellerV2Context(TELLER_V2).approveMarketForwarder(
            _marketId,
            SMART_COMMITMENT_FORWARDER
        );

        maxLoanDuration = _maxLoanDuration;
        interestRateLowerBound = _interestRateLowerBound;
        interestRateUpperBound = _interestRateUpperBound;

        require(interestRateLowerBound <= interestRateUpperBound, "invalid _interestRateLowerBound");

        require(_liquidityThresholdPercent <= 10000, "invalid _liquidityThresholdPercent"); 

        liquidityThresholdPercent = _liquidityThresholdPercent;
        collateralRatio = _collateralRatio;
        twapInterval = _twapInterval;
  
        poolSharesToken_ = _deployPoolSharesToken();
    }
```

Openzeppeline's `OwnableUpgradeable` specifically states to initialize below function in order to get the onlyOwner modifier functionality in contracts inheriting it.

```solidity
    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    function __Ownable_init() internal initializer {
        __Context_init_unchained();
        __Ownable_init_unchained();
    }

    function __Ownable_init_unchained() internal initializer {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }
```
    
Therefore, in current implementation it can be seen `__Ownable_init()` is not initlialized in `initialize()` function therefore the contract is ownerless since `OwnableUpgradeable` sets the deployer of contract as owner if it initialize the `__Ownable_init()`.

`onlyOwner` is used for below functionalities in `LenderCommitmentGroup_Smart.sol`,

1) `pauseBorrowing()`
2) `unpauseBorrowing()`

Both of these functions are used to pause and unpause the borrowing, However it can not be called by contract deployer due to the owner address by default would be zero address in current implementation. The function will create permanent deniel of service and will always revert if some other account calls it.

This breaks the core functionality of `LenderCommitmentGroup_Smart.sol` which is supposed to call above both functions by contract owner but it can not be called.

## Impact
It will not be possible to pause and unpause the borrowing in `LenderCommitmentGroup_Smart.sol` due to missing initialization of owner as discussed above. This breaks the core functionality of contract as the emergency stop mechanism will not be able to work. 

## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L65

## Tool used
Manual Review

## Recommendation
Initialize the `__Ownable_init()` in contract to make use of `onlyOwner()` functionality in contract so that both `pauseBorrowing()` and `unpauseBorrowing()` can be accessed by contract owner without any issues.

consider below changes:

```diff
    function initialize(
        address _principalTokenAddress,
        address _collateralTokenAddress,
        uint256 _marketId,
        uint32 _maxLoanDuration,
        uint16 _interestRateLowerBound,
        uint16 _interestRateUpperBound,
        uint16 _liquidityThresholdPercent, // When 100% , the entire pool can be drawn for lending.  When 80%, only 80% of the pool can be drawn for lending. 
        uint16 _collateralRatio, //the required overcollateralization ratio.  10000 is 1:1 baseline , typically this is above 10000
        uint24 _uniswapPoolFee,
        uint32 _twapInterval
    ) external initializer returns (address poolSharesToken_) {
        // require(!_initialized,"already initialized");
        // _initialized = true;
                                                                                                        
+     __Ownable_init();                        @audit // now initialized the owner       
        __Pausable_init();

        principalToken = IERC20(_principalTokenAddress);
        collateralToken = IERC20(_collateralTokenAddress);

        UNISWAP_V3_POOL = IUniswapV3Factory(UNISWAP_V3_FACTORY).getPool(
            _principalTokenAddress,
            _collateralTokenAddress,
            _uniswapPoolFee
        );

         . . . some code

        }
```