Plain Mulberry Goat

high

# Fee-on-Transfer Token will effect the complete accounting and result in DoS

## Summary
The `LenderCommitmentGroup_Smart` does not check for Fee-On-Transfer for principal tokens , this will result in wrong accounting and will create DoS and broken functionality.

## Vulnerability Detail
The `LenderCommitmentGroup_Smart` support every uniswapV3 pool tokens as Principal/Collateral tokens. So each pair ERC20 token is in scope which has Pool in unsiwapV3.This also include Fee-On-Transfer tokens.
 Following is flow of adding Principal tokens:
```solidity
    function addPrincipalToCommitmentGroup(
        uint256 _amount,
        address _sharesRecipient
    ) external returns (uint256 sharesAmount_) {
        //transfers the primary principal token from msg.sender into this contract escrow
        
        principalToken.transferFrom(msg.sender, address(this), _amount);

        sharesAmount_ = _valueOfUnderlying(_amount, sharesExchangeRate());

   @>     totalPrincipalTokensCommitted += _amount;
        //principalTokensCommittedByLender[msg.sender] += _amount;

        //mint shares equal to _amount and give them to the shares recipient !!!
        poolSharesToken.mint(_sharesRecipient, sharesAmount_);
    }
```
it can be seen from the above code we add the amount `totalPrincipalTokensCommitted += _amount` with out checking the actual amount received by contract and also calculate the amount of shareToken by using this amount value.
There are list of function which depends on `totalPrincipalTokensCommitted`  value to calculate different values like totalAvailableBalance to borrow and find Pool Utilization.
```solidity
function getPrincipalAmountAvailableToBorrow()
        public
        view
        returns (uint256)
    {     

            return  ( uint256( getPoolTotalEstimatedValue() )).percent(liquidityThresholdPercent) -
            getTotalPrincipalTokensOutstandingInActiveLoans() ;
     
    }

...

function getPoolTotalEstimatedValue()
        public
        view
        returns (uint256 poolTotalEstimatedValue_)
    {
       
         int256 poolTotalEstimatedValueSigned = int256(totalPrincipalTokensCommitted) 
         + int256(totalInterestCollected)  + int256(tokenDifferenceFromLiquidations) 
         - int256(totalPrincipalTokensWithdrawn);

        //if the poolTotalEstimatedValue_ is less than 0, we treat it as 0.  
        poolTotalEstimatedValue_ = poolTotalEstimatedValueSigned > int256(0)
            ? uint256(poolTotalEstimatedValueSigned)
            : 0;
    }

...
function getPoolUtilizationRatio() public view returns (uint16) {

        if (getPoolTotalEstimatedValue() == 0) {
            return 0;
        }

        return uint16(  Math.min(   
           getTotalPrincipalTokensOutstandingInActiveLoans()  * 10000  / 
           getPoolTotalEstimatedValue() , 10000  ));
    } 
... 
function getMinInterestRate() public view returns (uint16) {
        return interestRateLowerBound + uint16( uint256(interestRateUpperBound-interestRateLowerBound).percent(getPoolUtilizationRatio()) );
    }
``` 
`getMinInterestRate()` , `getPrincipalAmountAvailableToBorrow()`  and `sharesExchangeRate` will return wrong values which will efftect the Protcol in many ways.

## Impact
Due to wring calculations the Protocol will be insolvent until more tokens are not added directly to match `totalPrincipalTokensCommitted`. during this period the Rate will be calculate wrong, will return wrong available balance for borrow.
Even the shareToken minted will create insolvency which can not resolve form direct transfer.
## Code Snippet
[https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L317](https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L317)
[https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L288-L302](https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L288-L302)

## Tool used

Manual Review

## Recommendation
check the balance of contract before and after and add the difference to `totalPrincipalTokensCommitted`

```diff
diff --git a/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol b/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol
index 4b363d9..79b38f3 100644
--- a/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol
+++ b/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol
@@ -307,15 +307,18 @@ contract LenderCommitmentGroup_Smart is
     function addPrincipalToCommitmentGroup(
         uint256 _amount,
         address _sharesRecipient
+        // uint256 minAmountOut
     ) external returns (uint256 sharesAmount_) {
         //transfers the primary principal token from msg.sender into this contract escrow
-        
-        principalToken.transferFrom(msg.sender, address(this), _amount);
-
-        sharesAmount_ = _valueOfUnderlying(_amount, sharesExchangeRate());
+        uint256 balBefore = principalToken.balanceOf(address(this)); 
+        principalToken.transferFrom(msg.sender, address(this), _amount); // @audit : fee-on-transfer cal wrong , balanceBefor and BalanceAfter diff 
+_amount =    principalToken.balanceOf(address(this))-balBefore; 
 
+        sharesAmount_ = _valueOfUnderlying(_amount, sharesExchangeRate()); 
 
         //mint shares equal to _amount and give them to the shares recipient !!!
         poolSharesToken.mint(_sharesRecipient, sharesAmount_);

```