Fun Blonde Chicken

high

# `sharesExchangeRate()` may be zero causing Users mint zero shares token in `lenderCommitmentGroupSmart.addPrincipalToCommitmentGroup()`

## Summary
`sharesExchangeRate()` may be zero causing Users mint zero shares token in `lenderCommitmentGroupSmart.addPrincipalToCommitmentGroup()`
## Vulnerability Detail
`addPrincipalToCommitmentGroup` calls `_valueOfUnderlying` to calcute share amount.
```javascript
    function addPrincipalToCommitmentGroup(
        uint256 _amount,
        address _sharesRecipient
    ) external returns (uint256 sharesAmount_) {
        //transfers the primary principal token from msg.sender into this contract escrow
        
        principalToken.transferFrom(msg.sender, address(this), _amount);

@>        sharesAmount_ = _valueOfUnderlying(_amount, sharesExchangeRate());

        totalPrincipalTokensCommitted += _amount;
        //principalTokensCommittedByLender[msg.sender] += _amount;

        //mint shares equal to _amount and give them to the shares recipient !!!
        poolSharesToken.mint(_sharesRecipient, sharesAmount_);
    }
```
If rate is 0, `_valueOfUnderlying()` will return 0.
```javascript
   function _valueOfUnderlying(uint256 amount, uint256 rate)
        internal
        pure
        returns (uint256 value_)
    {
@>        if (rate == 0) {
            return 0;
        }

        value_ = (amount * EXCHANGE_RATE_EXPANSION_FACTOR) / rate;
    }
```
rate is `sharesExchangeRate()`, it can be zero. 
```javascript
function sharesExchangeRate() public view virtual returns (uint256 rate_) {
        

        uint256 poolTotalEstimatedValue = getPoolTotalEstimatedValue();

        if (poolSharesToken.totalSupply() == 0) {
            return EXCHANGE_RATE_EXPANSION_FACTOR; // 1 to 1 for first swap
        }

        rate_ =
            (poolTotalEstimatedValue  *
                EXCHANGE_RATE_EXPANSION_FACTOR) /
            poolSharesToken.totalSupply();
    }

```
Because poolTotalEstimatedValue can be zero
```javascript
 function getPoolTotalEstimatedValue()
        public
        view
        returns (uint256 poolTotalEstimatedValue_)
    {
       
@>         int256 poolTotalEstimatedValueSigned = int256(totalPrincipalTokensCommitted) 
         + int256(totalInterestCollected)  + int256(tokenDifferenceFromLiquidations) 
         - int256(totalPrincipalTokensWithdrawn);

        //if the poolTotalEstimatedValue_ is less than 0, we treat it as 0.  
        poolTotalEstimatedValue_ = poolTotalEstimatedValueSigned > int256(0)
            ? uint256(poolTotalEstimatedValueSigned)
            : 0;
    }
```
#### POC
- 1 `sharesExchangeRate()` is zero ,because pool lose some funds.
- 2 alice addPrincipalToCommitmentGroup with 1e18 PrincipalToken, mint 0 share token. 
```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

//import { LenderCommitmentGroup_Smart_Override } from "./LenderCommitmentGroup_Smart_Override.sol";
import {LenderCommitmentGroup_Smart} from "../../../../contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol";
import {TestERC20Token} from "../../../tokens/TestERC20Token.sol";
import "../../../../contracts/mock/WethMock.sol";
import "../../../../contracts/interfaces/IWETH.sol";

import {TellerV2SolMock} from "../../../../contracts/mock/TellerV2SolMock.sol";

import {UniswapV3PoolMock} from "../../../../contracts/mock/uniswap/UniswapV3PoolMock.sol";
import {UniswapV3FactoryMock} from "../../../../contracts/mock/uniswap/UniswapV3FactoryMock.sol";
import { PaymentType, PaymentCycleType } from "../../../../contracts/libraries/V2Calculations.sol";
import { LoanDetails, Payment, BidState , Bid, Terms } from "../../../../contracts/TellerV2Storage.sol";
import {console, Test} from  "lib/forge-std/src/Test.sol";


import { SmartCommitmentForwarder} from "../../../../contracts/LenderCommitmentForwarder/SmartCommitmentForwarder.sol";
import { TellerV2 } from "../../../../contracts/TellerV2.sol";
import { MarketRegistry } from "../../../../contracts/MarketRegistry.sol";
import { ReputationManager } from "../../../../contracts/ReputationManager.sol";
import "../../../../contracts/interfaces/IMarketRegistry.sol";
import "../../../../contracts/interfaces/IReputationManager.sol";
import {CollateralEscrowV1} from "../../../../contracts/escrow/CollateralEscrowV1.sol";
import  {UpgradeableBeacon} from  "@openzeppelin/contracts/proxy/beacon/UpgradeableBeacon.sol";
import {CollateralManager} from "../../../../contracts/CollateralManager.sol";
import { Collateral } from "../../../../contracts/interfaces/escrow/ICollateralEscrowV1.sol";
import { LenderManager } from "../../../../contracts/LenderManager.sol";
import { EscrowVault } from "../../../../contracts/EscrowVault.sol";
import {LenderCommitmentForwarder_G1} from "../../../../contracts/LenderCommitmentForwarder/LenderCommitmentForwarder_G1.sol";

import { User } from "../../../Test_Helpers.sol";

contract LenderCommitmentGroup_Smart_Real_Test is Test {

    TellerV2User private marketOwner;
    TellerV2User private extensionContract;

    TellerV2User private borrower;
    TellerV2User private lender;
    TellerV2User private liquidator;

    TestERC20Token principalToken;
    TestERC20Token collateralToken;
    WethMock wethMock;
    address poolSharesToken;

    LenderCommitmentGroup_Smart lenderCommitmentGroupSmart;
    TellerV2 tellerV2;
    CollateralManager collateralManager;

    uint256 marketId1;
    
    //TellerV2SolMock _tellerV2;
    SmartCommitmentForwarder smartCommitmentForwarder;
    UniswapV3PoolMock _uniswapV3Pool;
    UniswapV3FactoryMock _uniswapV3Factory;

    function setUp() public {
        vm.warp(0);
        wethMock = new WethMock();
     
////////////////////////////////////////////////////////////////////////////////////
        // Deploy Escrow beacon
        CollateralEscrowV1 escrowImplementation = new CollateralEscrowV1();
        UpgradeableBeacon escrowBeacon = new UpgradeableBeacon(
            address(escrowImplementation)
        );

        // Deploy protocol
        tellerV2 = new TellerV2(address(0));

        // Deploy MarketRegistry & ReputationManager
        IMarketRegistry marketRegistry = IMarketRegistry(new MarketRegistry());
        IReputationManager reputationManager = IReputationManager(
            new ReputationManager()
        );
        reputationManager.initialize(address(tellerV2));

        // Deploy Collateral manager
        collateralManager = new CollateralManager();
        collateralManager.initialize(address(escrowBeacon), address(tellerV2));

        // Deploy Lender manager
        // MetaForwarder metaforwarder = new MetaForwarder();
        // metaforwarder.initialize();
        LenderManager lenderManager = new LenderManager((marketRegistry));
        lenderManager.initialize();
        lenderManager.transferOwnership(address(tellerV2));

        EscrowVault escrowVault = new EscrowVault();
        escrowVault.initialize();

        // Deploy LenderCommitmentForwarder
        LenderCommitmentForwarder_G1 lenderCommitmentForwarder = new LenderCommitmentForwarder_G1(
                address(tellerV2),
                address(marketRegistry)
            );

        // Initialize protocol
        tellerV2.initialize(
            50,
            address(marketRegistry),
            address(reputationManager),
            address(lenderCommitmentForwarder),
            address(collateralManager),
            address(lenderManager),
            address(escrowVault)
        );

       
        // Instantiate users & balances
        marketOwner = new TellerV2User(address(tellerV2), wethMock);
        borrower = new TellerV2User(address(tellerV2), wethMock);
        lender = new TellerV2User(address(tellerV2), wethMock);
        liquidator = new TellerV2User(address(tellerV2), wethMock);


    // Create a market
        marketId1 = marketOwner.createMarket(
            address(marketRegistry),
            86400,    // _paymentCycleDuration,
            600, //_paymentDefaultDuration
            86400, //_bidExpirationTime
            500, //_feePercent
            false, //_requireLenderAttestation
            false, //_requireBorrowerAttestation
            PaymentType.EMI,
            PaymentCycleType.Seconds,
            ""
        );
    
////////////////////////////////////////////////////////////////////////////////////////
    
        smartCommitmentForwarder = new SmartCommitmentForwarder(address(tellerV2), address(marketRegistry));
        _uniswapV3Pool = new UniswapV3PoolMock();

        _uniswapV3Factory = new UniswapV3FactoryMock();
        _uniswapV3Factory.setPoolMock(address(_uniswapV3Pool));
 

        principalToken = TestERC20Token(address(wethMock)); //new TestERC20Token("wrappedETH", "WETH", 1e24, 18);
        wethMock.deposit{value: 1e24}();

        collateralToken = new TestERC20Token("PEPE", "pepe", 1e24, 18);

        principalToken.transfer(address(lender), 1e18);
        collateralToken.transfer(address(borrower), 1e18);


        _uniswapV3Pool.set_mockToken0(address(principalToken));
        _uniswapV3Pool.set_mockToken1(address(collateralToken));

        lenderCommitmentGroupSmart = new LenderCommitmentGroup_Smart(
            address(tellerV2),
            address(smartCommitmentForwarder),
            address(_uniswapV3Factory)
        );
    }

    function initialize_group_contract() public {
        address _principalTokenAddress = address(principalToken);
        address _collateralTokenAddress = address(collateralToken);
        uint32 _maxLoanDuration = 5000000;
        uint16 _minInterestRate = 100;
        uint16 _maxInterestRate = 800;
        uint16 _liquidityThresholdPercent = 10000;
        uint16 _loanToValuePercent = 10000;
        uint24 _uniswapPoolFee = 3000;
        uint32 _twapInterval = 5;
        
        //set trust
        vm.prank(address(marketOwner));
        tellerV2.setTrustedMarketForwarder(marketId1, address(smartCommitmentForwarder));
        
        poolSharesToken = lenderCommitmentGroupSmart.initialize(
            _principalTokenAddress,
            _collateralTokenAddress,
            marketId1,
            _maxLoanDuration,
            _minInterestRate,
            _maxInterestRate,
            _liquidityThresholdPercent,
            _loanToValuePercent,
            _uniswapPoolFee,
            _twapInterval
        );

    }
  
    function test_liquidateDefaultedLoanWithIncentive_MintZeroShareToken() public {


         initialize_group_contract();

         //lender deposit principal token in pool
        vm.startPrank(address(lender)); 
        principalToken.approve(address(lenderCommitmentGroupSmart), 1e18);
        lenderCommitmentGroupSmart.addPrincipalToCommitmentGroup(1e18, address(lender));
        vm.stopPrank();

        
        //borrow token form Group Smart Pool
        vm.startPrank(address(borrower));
        tellerV2.approveMarketForwarder(marketId1, address(smartCommitmentForwarder));
        //erc20 approve
        collateralToken.approve(address(collateralManager), 1e18);

      uint256 bidId =  smartCommitmentForwarder.acceptCommitmentWithRecipient(
            address(lenderCommitmentGroupSmart),
             1e18, 
             1e18, 
             0,
              address(collateralToken),
               address(borrower), 
               100, 
               86400);
        vm.stopPrank();

        vm.warp(86400 + 600 + 86400 + 10000 ); //after 86400 + 600 + 86400 + 10000 seconds to liquidate

        Payment memory due = tellerV2.calculateAmountDue(bidId, block.timestamp);
       
        //Preparing for liquidation
        vm.startPrank(address(liquidator));
        lenderCommitmentGroupSmart.liquidateDefaultedLoanWithIncentive(bidId, -1e18);
        vm.stopPrank();
        
        assertEq(principalToken.balanceOf(address(lenderCommitmentGroupSmart)), 0);
        assertEq(principalToken.balanceOf(address(liquidator)), 0);

        //sharesExchangeRate can be zero
        assertEq(lenderCommitmentGroupSmart.sharesExchangeRate(),0);
        //alice add 1e18,but get 0 
        TellerV2User alice = new TellerV2User(address(tellerV2), wethMock);
        principalToken.transfer(address(alice), 1e18);
        vm.startPrank(address(alice));
        principalToken.approve(address(lenderCommitmentGroupSmart), 1e18);
        lenderCommitmentGroupSmart.addPrincipalToCommitmentGroup(1e18, address(alice));
        vm.stopPrank();

        assertEq(IERC20(poolSharesToken).balanceOf(address(alice)),0);

      }

}

contract TellerV2User is User {
    WethMock public immutable wethMock;

    constructor(address _tellerV2, WethMock _wethMock) User(_tellerV2) {
        wethMock = _wethMock;
    }

    function depositToWeth(uint256 amount) public {
        wethMock.deposit{ value: amount }();
    }
}
```
Create a new file named LenderCommitmentGroup_Smart_MintZeroShareToken.sol with code above in the fold `./packages/contracts/tests/LenderCommitmentForwarder/extensions/SmartCommitments`, Then run ` forge test --mt test_liquidateDefaultedLoanWithIncentive_MintZeroShareToken  -vvv` in terminal. you will get:

```bash
[PASS] test_liquidateDefaultedLoanWithIncentive_MintZeroShareToken() (gas: 3243336)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 6.97ms (1.45ms CPU time)

Ran 1 test suite in 178.71ms (6.97ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
User loses funds when `sharesExchangeRate()` is zero

## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L262C2-L275C6
## Tool used

Manual Review

## Recommendation
Shoud not let user to call `addPrincipalToCommitmentGroup` when `sharesExchangeRate()` is zero