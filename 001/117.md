Jolly Bamboo Goose

high

# burnSharesToWithdrawEarnings burns shares before calculating the principal token amount to withdraw

## Summary
The burnSharesToWithdrawEarnings at LenderCommitmentGroup contract burns the shares before calculating the principal token value to withdraw. The issue with that is it decreases the totalSupply of shares, therefore distributing more tokens than it should to the caller.
## Vulnerability Detail
The a look at burnSharesToWithdrawEarnings implementation:
```solidity
function burnSharesToWithdrawEarnings(
        uint256 _amountPoolSharesTokens,
        address _recipient
    ) external returns (uint256) {
 
        poolSharesToken.burn(msg.sender, _amountPoolSharesTokens);

        uint256 principalTokenValueToWithdraw = _valueOfUnderlying(
            _amountPoolSharesTokens,
            sharesExchangeRateInverse()
        );

        totalPrincipalTokensWithdrawn += principalTokenValueToWithdraw;

        principalToken.transfer(_recipient, principalTokenValueToWithdraw);

        return principalTokenValueToWithdraw;
    }
```

First it burns the pool shares then it calculates value of underlying.
This is wrong, as value of underlying relies on the exchange rate:
```solidity
function _valueOfUnderlying(uint256 amount, uint256 rate)
        internal
        pure
        returns (uint256 value_)
    {
        if (rate == 0) {
            return 0;
        }

        value_ = (amount * EXCHANGE_RATE_EXPANSION_FACTOR) / rate;
    }
```

And sharesExchangeRateInverse too. It becomes smaller as sharesExchangeRate becomes bigger due to the decrease in the totalSupply that has happened when the pool shares were burnt:
```solidity
function sharesExchangeRateInverse()
        public
        view
        virtual
        returns (uint256 rate_)
    {
        return
            (EXCHANGE_RATE_EXPANSION_FACTOR * EXCHANGE_RATE_EXPANSION_FACTOR) /
            sharesExchangeRate();
    }
```
## Impact
The amount of principal tokens that will be withdrawn is bigger than it should, as it calculates the tokens to be sent based on an artificially deflated amount of shares.

In a bad scenario, the user will attempt to burn all the last shares available at the pool. This means the burnSharesToWithdrawEarnings will revert as the call will involve divisions by zero.

In the worst case scenario, a user can withdraw its shares, receive more principal than it should, deposit this inflated principal to receive more shares than it had beforehand and repeat this loop of actions till the whole contract is drained.
## Code Snippet
[2024-04-teller-finance/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol at main · sherlock-audit/2024-04-teller-finance (github.com)](https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L396)

[2024-04-teller-finance/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol at main · sherlock-audit/2024-04-teller-finance (github.com)](https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L324)

[2024-04-teller-finance/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol at main · sherlock-audit/2024-04-teller-finance (github.com)](https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L277)

## Tool used
Manual Review
## Recommendation
Make sure to calculate the value of underlying tokens before burning any pool shares. This will ensure a truthy amount of principal tokens are to be withdrawn.
