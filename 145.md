Silly Linen Turtle

medium

# Incorrect calculation of `_borrowerAmount` in `FlashRolloverLoan_G5.calculateRolloverAmount()`.

## Summary

`FlashRolloverLoan_G5.calculateRolloverAmount()` returns the `_borrowerAmount` with reversed sign.

## Vulnerability Detail

At `L422` of `FlashRolloverLoan_G5.calculateRolloverAmount()`, there is the calculation of the `_borrowerAmount`.

```solidity
    function calculateRolloverAmount(
        address _lenderCommitmentForwarder,
        uint256 _loanId,
        AcceptCommitmentArgs calldata _commitmentArgs,
        uint16 _flashloanPremiumPct,
        uint256 _timestamp
    ) external view returns (uint256 _flashAmount, int256 _borrowerAmount) {
        Payment memory repayAmountOwed = TELLER_V2.calculateAmountOwed(
            _loanId,
            _timestamp
        );

        uint256 _marketId = _getMarketIdForCommitment(_lenderCommitmentForwarder,
            _commitmentArgs.commitmentId
        );
        uint16 marketFeePct = _getMarketFeePct(_marketId);
        uint16 protocolFeePct = _getProtocolFeePct();

        uint256 commitmentPrincipalRequested = _commitmentArgs.principalAmount;
        uint256 amountToMarketplace = commitmentPrincipalRequested.percent(
            marketFeePct
        );
        uint256 amountToProtocol = commitmentPrincipalRequested.percent(
            protocolFeePct
        );

        uint256 commitmentPrincipalReceived = commitmentPrincipalRequested -
            amountToMarketplace -
            amountToProtocol;

        // by default, we will flash exactly what we need to do relayLoanFull
        uint256 repayFullAmount = repayAmountOwed.principal +
            repayAmountOwed.interest;

        _flashAmount = repayFullAmount;
        uint256 _flashLoanFee = _flashAmount.percent(_flashloanPremiumPct);

422     _borrowerAmount =
            int256(commitmentPrincipalReceived) -
            int256(repayFullAmount) -
            int256(_flashLoanFee);
    }
```

In fact, `_borrowerAmount` is the additional amount for the borrower to use to pay back to flashloan pool at the end of the rollover transaction. So, it should be the flashloaned value subtracted by newly borrowed amount(`commitmentPrincipalReceived`).

## Impact

`FlashRolloverLoan_G5.calculateRolloverAmount()` returns a negative amount, even though the borrower has to pay some additional amounts, leading to the reversal of the rollover transaction.

## Code Snippet

https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/FlashRolloverLoan_G5.sol#L385-L426

## Tool used

Manual Review

## Recommendation

The calculation of the `_borrowerAmount` should be fixed as follows.

```diff
    function calculateRolloverAmount(
        address _lenderCommitmentForwarder,
        uint256 _loanId,
        AcceptCommitmentArgs calldata _commitmentArgs,
        uint16 _flashloanPremiumPct,
        uint256 _timestamp
    ) external view returns (uint256 _flashAmount, int256 _borrowerAmount) {
        Payment memory repayAmountOwed = TELLER_V2.calculateAmountOwed(
            _loanId,
            _timestamp
        );

        uint256 _marketId = _getMarketIdForCommitment(_lenderCommitmentForwarder,
            _commitmentArgs.commitmentId
        );
        uint16 marketFeePct = _getMarketFeePct(_marketId);
        uint16 protocolFeePct = _getProtocolFeePct();

        uint256 commitmentPrincipalRequested = _commitmentArgs.principalAmount;
        uint256 amountToMarketplace = commitmentPrincipalRequested.percent(
            marketFeePct
        );
        uint256 amountToProtocol = commitmentPrincipalRequested.percent(
            protocolFeePct
        );

        uint256 commitmentPrincipalReceived = commitmentPrincipalRequested -
            amountToMarketplace -
            amountToProtocol;

        // by default, we will flash exactly what we need to do relayLoanFull
        uint256 repayFullAmount = repayAmountOwed.principal +
            repayAmountOwed.interest;

        _flashAmount = repayFullAmount;
        uint256 _flashLoanFee = _flashAmount.percent(_flashloanPremiumPct);

        _borrowerAmount =
-           int256(commitmentPrincipalReceived) -
-           int256(repayFullAmount) -
-           int256(_flashLoanFee);
+           int256(repayFullAmount) +
+           int256(_flashLoanFee) -
+           int256(commitmentPrincipalReceived);
    }
```