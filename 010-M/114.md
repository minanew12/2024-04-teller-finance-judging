Eager Tan Wombat

medium

# Users will lose funds when trying to add principal to commitment group due to retruned 0 rate


## Summary
Users who deposit when there's a negative `poolTotalEstimatedValue_` will lose their principal tokens in return for zero shares.

## Vulnerability Detail
When the `poolTotalEstimatedValue_` is calculated through the `getPoolTotalEstimatedValue` function, it checks if the value is < 0. If it is, the function returns zero as a result.
```solidity
        //if the poolTotalEstimatedValue_ is less than 0, we treat it as 0.  
        poolTotalEstimatedValue_ = poolTotalEstimatedValueSigned > int256(0) //will return 0
            ? uint256(poolTotalEstimatedValueSigned)
            : 0;
    }
```
Futhermore, the value is used to calculate the `rate`, through the `sharesExchangeRate` formula. Which will cause that rate be 0.
```solidity
    function sharesExchangeRate() public view virtual returns (uint256 rate_) {
        

        uint256 poolTotalEstimatedValue = getPoolTotalEstimatedValue(); //@note will return 0

        if (poolSharesToken.totalSupply() == 0) {
            return EXCHANGE_RATE_EXPANSION_FACTOR; // 1 to 1 for first swap
        }

        rate_ =
            (poolTotalEstimatedValue  *
                EXCHANGE_RATE_EXPANSION_FACTOR) /
            poolSharesToken.totalSupply();
    }
```

Finally, when users call the `addPrincipalToCommitmentGroup` function, transferring in principal tokens to the contract, the 0 value rate will be used that way to calculate the `sharesAmount_` by calling the `_valueOfUnderlying` function. 

```solidity
    function addPrincipalToCommitmentGroup(
        uint256 _amount,
        address _sharesRecipient
    ) external returns (uint256 sharesAmount_) {
        //transfers the primary principal token from msg.sender into this contract escrow
        
        principalToken.transferFrom(msg.sender, address(this), _amount);

        sharesAmount_ = _valueOfUnderlying(_amount, sharesExchangeRate());

        totalPrincipalTokensCommitted += _amount;
        //principalTokensCommittedByLender[msg.sender] += _amount;

        //mint shares equal to _amount and give them to the shares recipient !!!
        poolSharesToken.mint(_sharesRecipient, sharesAmount_);
    }
```

And as can be seen from the `_valueOfUnderlying` function, if the rate is zero, the returned value is 0.

```solidity
    function _valueOfUnderlying(uint256 amount, uint256 rate)
        internal
        pure
        returns (uint256 value_)
    {
        if (rate == 0) {
            return 0;
        }

        value_ = (amount * EXCHANGE_RATE_EXPANSION_FACTOR) / rate;
    }
```

At this point, the `addPrincipalToCommitmentGroup` becomes a sort of honeypot as any users who calls the function will essentially lose their tokens getting no shares in retur.

## Impact
Unexpected loss of funds for users as they send principal tokens to the contract but receive no pool shares in return.
## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L324
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L315
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L262
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L299

## Tool used
Manual Code Review

## Recommendation
COnsider checking if `_valueOfUnderlying` is zero and reverting if it is.
