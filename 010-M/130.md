Spare Hazel Cougar

high

# Users could lose out on all their tokens when attempting to add more principal to the commitment group

## Summary

In some cases users would lose out on all their tokens when attempting to add more principal to the commitment group

## Vulnerability Detail

First take a look at https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L262-L275

```solidity

    function sharesExchangeRate() public view virtual returns (uint256 rate_) {
        //@audit
        uint256 poolTotalEstimatedValue = getPoolTotalEstimatedValue();
        if (poolSharesToken.totalSupply() == 0) {
            return EXCHANGE_RATE_EXPANSION_FACTOR; // 1 to 1 for first swap
        }
        rate_ =
            (poolTotalEstimatedValue  *
                EXCHANGE_RATE_EXPANSION_FACTOR) /
            poolSharesToken.totalSupply();
    }
```

This function determines the number of shares one gets for depositing principal tokens and the number of principal tokens one can receive receive for burning shares.

Would be key to note that it makes a query to ` getPoolTotalEstimatedValue()` that can return `0` in the case where the estimated value is currently negative, see https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L287-L303

```solidity
    }

    function getPoolTotalEstimatedValue()
        public
        view
        returns (uint256 poolTotalEstimatedValue_)
    {

         int256 poolTotalEstimatedValueSigned = int256(totalPrincipalTokensCommitted)
         + int256(totalInterestCollected)  + int256(tokenDifferenceFromLiquidations)
         - int256(totalPrincipalTokensWithdrawn);

        //if the poolTotalEstimatedValue_ is less than 0, we treat it as 0.
        poolTotalEstimatedValue_ = poolTotalEstimatedValueSigned > int256(0)
            ? uint256(poolTotalEstimatedValueSigned)
            : 0;
    }
```

Having the be `0` makes the calculation below `0` too , i.e the rate gotten from `sharesExchangeRate()` would essentially be zero too.

```solidity
  rate_ =
            (poolTotalEstimatedValue  *
                EXCHANGE_RATE_EXPANSION_FACTOR) /
            poolSharesToken.totalSupply();
```

Now see the function used whenever anyone is attemmpting to add more principal to the commitment group https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L307-L333

```solidity
    function addPrincipalToCommitmentGroup(
        uint256 _amount,
        address _sharesRecipient
    ) external returns (uint256 sharesAmount_) {
        //transfers the primary principal token from msg.sender into this contract escrow
        principalToken.transferFrom(msg.sender, address(this), _amount);
         //@audit
        sharesAmount_ = _valueOfUnderlying(_amount, sharesExchangeRate());

        totalPrincipalTokensCommitted += _amount;
        //principalTokensCommittedByLender[msg.sender] += _amount;

        //mint shares equal to _amount and give them to the shares recipient !!!
        poolSharesToken.mint(_sharesRecipient, sharesAmount_);
    }

        function _valueOfUnderlying(uint256 amount, uint256 rate)
        internal
        pure
        returns (uint256 value_)
    {
        if (rate == 0) {
            return 0;
        }
        //@audit
        value_ = (amount * EXCHANGE_RATE_EXPANSION_FACTOR) / rate;
    }
```

We can see that the value returned from `sharesExchangeRate()` directly has an impact on the amount of shares that get minted to the user, i.e:

- When `sharesExchangeRate()` returns `0`, the `value` gotten from `_valueOfUnderlying()` is also going to be zero, i.e `value_ = (amount * EXCHANGE_RATE_EXPANSION_FACTOR) / rate`.
- Which then causes the share amount `sharesAmount_ = _valueOfUnderlying(_amount, sharesExchangeRate())` to be zero from `addPrincipalToCommitmentGroup()`
- And then zero shares gets minted to the user adding in the principal to the commitment group via `poolSharesToken.mint(_sharesRecipient, sharesAmount_);`

## Impact

Anybody that deposits when the `poolTotalEstimatedValue` is `negative` loses all their principal tokens and receives 0 shares, since someone that passes any amount whatsoever to the `addPrincipalToCommitmentGroup()` function will completely lose his tokens when the `sharesExchangeRate`, keep in mind that even if the amount they were to provide was enormous and would lead to the pool estimated value to become positive, they still recieve `0` shares.

> Assume a minimalistic scenario with logic: protocol has received/is supposed to have assets worth $12000 usd, market fluctuation and what not makes the `poolTotalEstimatedValue` to be negative, say ` - $1200` now the returned value from the `sharesExchangeRate` would be zero, even if a user is passing on `$1500` worth of assets via `addPrincipalToCommitmentGroup` which in its sense should definitely lead to the `poolTotalEstimatedValue` being ovely +ve since the `totalPrincipalTokensCommitted` variable in this case would be updated with amount to be added, the user is still going to be minted only **0 shares**... _(this is just a scenario using logic and can be subsided and the report would still stand)_.

## Code Snippet

https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L262-L275

https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L307-L333

## Tool used

Manual Review

## Recommendation

Consider reimplementing the contract's logic for whenever the pool estimated value goes negative
