Gigantic Carmine Viper

high

# Liquidating a loan will leave the liquidator at a loss

## Summary
Users can use the `liquidateDefaultedLoanWithIncentive()` function to liquidate a defaulted loan and get an incentive. However, this will not only not have any incentive for the liquidator but will even cost him money.
## Vulnerability Detail
Upon calling of the `liquidateDefaultedLoanWithIncentive()` function, first of all, there are some calculations to decide the amount of money the contract will receive from the liquidator, these are not important for this issue however it is important to note that the liquidator will have to pay out the defaulted loan (or at least a part of it). 
```solidity
function liquidateDefaultedLoanWithIncentive(
        uint256 _bidId,
        int256 _tokenAmountDifference
    ) public bidIsActiveForGroup(_bidId) {
        uint256 amountDue = getAmountOwedForBid(_bidId, false);

        uint256 loanDefaultedTimeStamp = ITellerV2(TELLER_V2)
            .getLoanDefaultTimestamp(_bidId);

        int256 minAmountDifference = getMinimumAmountDifferenceToCloseDefaultedLoan(
                amountDue,
                loanDefaultedTimeStamp
            );

        require(
            _tokenAmountDifference >= minAmountDifference, 
            "Insufficient tokenAmountDifference"
        );

        if (_tokenAmountDifference > 0) {
            //this is used when the collateral value is higher than the principal (rare)
            //the loan will be completely made whole and our contract gets extra funds too
            uint256 tokensToTakeFromSender = abs(_tokenAmountDifference);

            IERC20(principalToken).transferFrom(
                msg.sender,
                address(this),
                amountDue + tokensToTakeFromSender
            );

            tokenDifferenceFromLiquidations += int256(tokensToTakeFromSender);

            totalPrincipalTokensRepaid += amountDue;
        } else { 
           
            uint256 tokensToGiveToSender = abs(_tokenAmountDifference);

            IERC20(principalToken).transferFrom(
                msg.sender,
                address(this),
                amountDue - tokensToGiveToSender
            );

            tokenDifferenceFromLiquidations -= int256(tokensToGiveToSender);

            totalPrincipalTokensRepaid += amountDue;
        }

        //this will give collateral to the caller
        ITellerV2(TELLER_V2).lenderCloseLoanWithRecipient(_bidId, msg.sender);
    }
```
Then, `lenderCloseLoanWithRecipient()` gets called which then calls the internal `_lenderCloseLoanWithRecipient()`. Both are called with the `_bidId` and the `_collateralRecipient` which here is equal to the liquidator. That function should give the collateral to the liquidator as first of all visible by the last comment in the function above as well as the goal of the function, the liquidator pays out the defaulted loan and gets the collateral in return which could obviously financialy make sense for a user to do in certain scenarios.
```solidity
function _lenderCloseLoanWithRecipient(
        uint256 _bidId,
        address _collateralRecipient
    ) internal acceptedLoan(_bidId, "lenderClaimCollateral") {
        require(isLoanDefaulted(_bidId), "Loan must be defaulted.");

        Bid storage bid = bids[_bidId];
        bid.state = BidState.CLOSED;

        address sender = _msgSenderForMarket(bid.marketplaceId);
        require(sender == bid.lender, "only lender can close loan");

        collateralManager.lenderClaimCollateral(_bidId);

        emit LoanClosed(_bidId);
    }
```
The issue arises by the fact that the `_collateralRecipient` is not used at all. The collateral is just transferred to the loan lender by calling the `_withdraw()` as visible by the called `lenderClaimCollateral()` function, both in the `CollateralManager` contract:
```solidity
function lenderClaimCollateral(uint256 _bidId) external onlyTellerV2 {
        if (isBidCollateralBacked(_bidId)) {
            BidState bidState = tellerV2.getBidState(_bidId);

            require(
                bidState == BidState.CLOSED,
                "Loan has not been liquidated"
            );

            _withdraw(_bidId, tellerV2.getLoanLender(_bidId));
            emit CollateralClaimed(_bidId);
        }
    }
```
Of course, this means that not only the liquidator will pay for the liquidation but he will also receive absolutely nothing.
## Impact
Liquidator will not only not get money from the liquidation but he will actually lose money.
## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L422-L472
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/TellerV2.sol#L745-L774
https://github.com/sherlock-audit/2024-04-teller-finance/blob/defe55469a2576735af67483acf31d623e13592d/teller-protocol-v2-audit-2024/packages/contracts/contracts/CollateralManager.sol#L271-L283
## Tool used

Manual Review

## Recommendation
Use the `_collateralRecipient` variable.