Overt Denim Alligator

high

# Stealing principal tokens from the protocol

## Summary

In the function `addPrincipalToCommitmentGroup()`, the protocol calls `transferFrom()` to withdraw funds from the user, and then calculates shares to mint to the user based on the amount transferred. However, the protocol does not validate the return value of `transferFrom()`. Some tokens do not revert on transfer failure; instead, they return false. Malicious users can exploit this by minting an unlimited amount of shares after a failed transfer and then burning them to withdraw funds from the protocol.

## Vulnerability Detail

The function `LenderCommitmentGroup_Smart.addPrincipalToCommitmentGroup()` allows users to deposit principal tokens and receive poolShares tokens in return. In this function, the protocol calls `token.transferFrom()` to withdraw principal tokens from the user, and then calculates shares based on the amount of tokens deposited by the user, minting them to the user. 
```solidity

 function addPrincipalToCommitmentGroup(
        uint256 _amount,
        address _sharesRecipient
    ) external returns (uint256 sharesAmount_) {
        //transfers the primary principal token from msg.sender into this contract escrow
        
        principalToken.transferFrom(msg.sender, address(this), _amount);

        sharesAmount_ = _valueOfUnderlying(_amount, sharesExchangeRate());

        totalPrincipalTokensCommitted += _amount;
        //principalTokensCommittedByLender[msg.sender] += _amount;

        //mint shares equal to _amount and give them to the shares recipient !!!
        poolSharesToken.mint(_sharesRecipient, sharesAmount_);
    }

```

According to the documentation, the protocol supports any token integrated with Uniswap V3. 
```solidity
We are allowing any standard token that would be compatible with Uniswap V3 to work with our codebase, just as was the case for the original audit of TellerV2.sol . The tokens are assumed to be able to work with Uniswap V3 

```

However, some tokens like ZRX do not throw an error when `transferFrom()` fails, instead, they return false. 
```solidity
    function transferFrom(address _from, address _to, uint _value) returns (bool) {
        if (balances[_from] >= _value && allowed[_from][msg.sender] >= _value && balances[_to] + _value >= balances[_to]) {
            balances[_to] += _value;
            balances[_from] -= _value;
            allowed[_from][msg.sender] -= _value;
            Transfer(_from, _to, _value);
            return true;
        } else { return false; }
    }

```

These tokens are paired with ETH on Uniswap V3 to form a pool.
https://info.uniswap.org/#/pools/0x14424eeecbff345b38187d0b8b749e56faa68539

Therefore, malicious actors can deposit an amount of these tokens for which they do not have sufficient balance, causing `transferFrom()` to fail and return false. Since the protocol does not validate the return value, malicious actors can obtain shares without depositing any tokens. Subsequently, they can call `burnSharesToWithdrawEarnings()` to burn the shares and withdraw funds from the protocol.

## Impact
Stealing principal tokens from the protocol

## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L313
## Tool used

Manual Review

## Recommendation
Using OpenZeppelin's `safeTransferFrom()` instead of `transferFrom()`
